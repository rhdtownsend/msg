! Module   : make_passband
! Purpose  : create passband files from ASCII tables
!
! Copyright 2021-2025 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

program make_passband

   ! Uses

   use forum_m
   use msg_m

   use tools_utils_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Variables

   character(:), allocatable :: response_file_name
   real(RD), allocatable     :: F_0
   character(:), allocatable :: passband_file_name

   type(arg_parser_t)    :: arg_parser
   integer               :: unit
   integer               :: n
   real(RD), allocatable :: lam(:)
   real(RD), allocatable :: S(:)
   integer               :: i
   type(passband_t)      :: passband
   type(hdf5io_t)        :: hdf5io

   ! Parse command-line arguments

   arg_parser = arg_parser_t('make_passband RESPONSE_FILE PASSBAND_FILE', auto_help=.TRUE.)

   call arg_parser%define_option('zero-point', OPT_REQUIRED_ARG, short_name='z', &
      usage='--zero-point=F_0', description='zero-point flux of the photometric system')

   call arg_parser%parse(arg_proc, opt_proc)

   if (.NOT. ALLOCATED(response_file_name)) call print_summary()
   if (.NOT. ALLOCATED(passband_file_name)) call print_summary()

   if (.NOT. ALLOCATED(F_0)) then
      @:STOP('--zero-point must be specified')
   end if

   ! Read the input file

   open(NEWUNIT=unit, STATUS='OLD', FILE=response_file_name)

   n = 0

   count_loop : do
      read(unit, *, END=100)
      n = n + 1
   end do count_loop

100 continue

   rewind(unit)

   allocate(lam(n))
   allocate(S(n))

   read_loop : do i = 1, n
      read(unit, *) lam(i), S(i)
   end do read_loop

   close(unit)

   ! Create the passband

   passband = passband_t(S, lam, F_0)

   ! Write the output file

   hdf5io = hdf5io_t(passband_file_name, CREATE_FILE)
   call passband%write(hdf5io)
   call hdf5io%final()

   ! Finish

contains

   subroutine arg_proc(a, value)

      integer, intent(in)      :: a
      character(*), intent(in) :: value

      select case(a)
      case(1)
         response_file_name = value
      case(2)
         passband_file_name = value
      case default
         call print_summary()
      end select

   end subroutine arg_proc

   !****

   subroutine opt_proc(name, value)

      character(*), intent(in) :: name
      character(*), intent(in) :: value

      integer :: stat

      stat = STAT_OK

      select case(name)
      case('zero-point')
         call parse_value(value, F_0, stat)
      case('help')
         call print_summary()
      case default
         @:ABORT('invalid option name')
      end select

      if (stat /= STAT_OK) then
         @:STOP('invalid --'//name)
      end if

   end subroutine opt_proc

   !****

   subroutine print_summary()

      call arg_parser%print_summary()
      @:STOP()

   end subroutine print_summary

end program make_passband
